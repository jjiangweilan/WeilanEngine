// Reference: https://cdn2.unrealengine.com/Resources/files/2013SiggraphPresentationsNotes-26915738.pdf
#version 450
#include "Common/Common.glsl"
#if G_VSM
#define VSM_SHADOW_MAP_SAMPLING_PASS
#include "Game/Shadow/VSM.glsl"
#endif
#include "VirtualTexture/VirtualTexture.glsl"
#define M_PI 3.1415926535897932384626433832795

#if CONFIG
name: Game/StandardPBR
features:
    - [G_PCF, G_VSM]
    - [G_, G_SURFEL_BAKE]
#endif

#define LOC_ShadowMapCS 5

#if VERT
layout(location = 0) in vec3 i_Position;
layout(location = 1) in vec3 i_Normal;
layout(location = 2) in vec2 i_UV;

layout(location = 0) out vec3 o_PositionWS;
layout(location = 1) out vec3 o_NormalWS;
layout(location = 2) out vec3 o_TangentWS;
layout(location = 3) out vec3 o_BitangentWS;
layout(location = 4) out vec2 o_UV;
layout(location = LOC_ShadowMapCS) out vec4 o_ShadowCS;
void main()
{
    vec3 tangent = vec3(1,0,0);
    o_PositionWS = vec3(pconst.model * vec4(i_Position, 1));
    o_NormalWS = (inverse(transpose(pconst.model)) * vec4(i_Normal,1)).xyz;
    o_TangentWS = (inverse(transpose(pconst.model)) * vec4(tangent,1)).xyz;
    o_BitangentWS = cross(o_NormalWS, o_TangentWS);
    o_UV = i_UV;

    o_ShadowCS = scene.worldToShadow * vec4(o_PositionWS, 1);

    gl_Position = scene.viewProjection * vec4(o_PositionWS, 1);
}
#endif

#if FRAG
layout(location = 0) in vec3 i_PositionWS;
layout(location = 1) in vec3 i_NormalWS;
layout(location = 2) in vec3 i_TangentWS;
layout(location = 3) in vec3 i_BitangentWS;
layout(location = 4) in vec2 i_UV;
layout(early_fragment_tests) in;

layout(location = LOC_ShadowMapCS) in vec4 i_ShadowCS;

layout(location = 0) out vec4 o_Color;
#ifdef G_SURFEL_BAKE
layout(location = 1) out vec4 o_Normal;
layout(location = 2) out vec4 o_Position;
#endif

#define BaseColorTex baseColorTex_sampler_linear
#define NormalMap normalMap_sampler_linear
#define EmissiveMap emissiveMap_sampler_linear
#define MetallicRoughnessMap metallicRoughnessMap_sampler_linear

layout(set = SET_MATERIAL, binding = 0) uniform PBR
{
    vec4 baseColorFactor;
    float roughness;
    float metallic;
} matParams;
layout(set = SET_MATERIAL, binding = 1) uniform sampler2D BaseColorTex;
layout(set = SET_MATERIAL, binding = 2) uniform sampler2D NormalMap;
layout(set = SET_MATERIAL, binding = 3) uniform sampler2D MetallicRoughnessMap;
layout(set = SET_MATERIAL, binding = 4) uniform sampler2D EmissiveMap;
layout(set = SET_MATERIAL, binding = 5) uniform sampler _sampler_point;
#define Roughness matParams.roughness
#define Metallic matParams.metallic
#define NearZero 0.000001

float pow2(float x)
{
    return x * x;
}

const vec2 invAtan = vec2(0.1591, 0.3183);
vec2 SphericalToUV(vec3 v)
{
    // -asin because we have top-left uv origin
    vec2 uv = vec2(atan(v.z, v.x), -asin(v.y));

    // convert to [0,1]
    uv *= invAtan;
    uv += 0.5;
    return uv;
}

float SpecularBRDF(vec3 l, vec3 v, vec3 n, float metallic, float roughness)
{
    vec3 h = normalize(l + v);
    float dotNH = max(dot(n,h), NearZero);
    float dotNV = max(dot(n,v), NearZero);
    float dotNL = max(dot(n,l), NearZero);
    float dotVH = max(dot(v,h), NearZero);
    float r2 = roughness * roughness;
    r2 *= r2;
// ndf
    float D = r2 / (M_PI * pow2(pow2(dotNH) * (r2 - 1) + 1));

// geometry
    float k = pow2(roughness + 1) / 8;
    float Gv = dotNV / (dotNV * (1 - k) + k);
    float Gl = dotNL / (dotNL * (1 - k) + k);
    float G = Gv * Gl;

// fresnel
    float F = metallic + (1 - metallic) * pow(2, (-5.55472 * dotVH - 6.98316) * dotVH);

    float brdf = (G * F * D) / (4 * dotNL * dotNV);
    return brdf;
}

void main()
{
// get normal
    vec3 tangent = normalize(i_TangentWS);
    vec3 normal = normalize(i_NormalWS);
    vec3 bitangent = normalize(i_BitangentWS);
    mat3 TBN = {tangent, bitangent, normal};
    vec3 normalMap = texture(NormalMap, i_UV).xyz;
    normal = TBN * normalMap;

    vec2 metallicRoughness = vec2(Roughness, Metallic) * texture(MetallicRoughnessMap, i_UV).gb;
    float metallic = metallicRoughness.y;
    float roughness = metallicRoughness.x;
    vec3 color = vec3(0,0,0);

    vec3 baseColor = texture(BaseColorTex, i_UV).xyz / M_PI * matParams.baseColorFactor.xyz;
    vec3 v = normalize(scene.viewPos.xyz - i_PositionWS);
    vec3 n = normalize(normal);

    for (int i = 0; i < scene.lightCount.x; ++i)
    {
        vec4 lightPos = scene.lights[i].position;
        vec3 l = lightPos.xyz;

        float dotNL = max(dot(n,l), NearZero);
        float brdf = SpecularBRDF(l, v, n, metallic, roughness);

        float light = dotNL * scene.lights[i].intensity;
        color += light * (baseColor / M_PI + brdf);
    }

    // shadow mask
    vec2 shadowUV = i_ShadowCS.xy / i_ShadowCS.w * 0.5 + 0.5;
    float objShadowDepth = i_ShadowCS.z / i_ShadowCS.w;
    float shadowAttenuation = 1.0;
    if (shadowUV.x < 1 && shadowUV.y < 1 && shadowUV.x > 0 && shadowUV.y > 0)
    {
#ifdef G_VSM
        shadowAttenuation = VSMShadowAttenuation(shadowUV, objShadowDepth, 0.0001, 0.2);
#else
        shadowAttenuation = PcfShadow(shadowUV, objShadowDepth);
#endif
    }

    vec3 emissive = texture(emissiveMap_sampler_linear, i_UV).xyz;

    o_Color = vec4(color * shadowAttenuation + emissive, 1);
#ifdef G_SURFEL_BAKE
    o_Normal = vec4(n, 1.0f);
    o_Position = vec4(i_PositionWS.xyz, 1.0f);
#endif
}
#endif


