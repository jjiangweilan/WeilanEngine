#version 450

#if CONFIG
name: Game/Fluid/Fog
#endif

#if COMP

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(set = 0, r32f) uniform image3D imgOutput;
layout(set = 0) uniform Global
{
    float time;
} g;

layout(set = 2) uniform Fog
{
    vec4 worley_freq;
    vec4 perlin0_freq;
    vec4 perlin0_amp;

    float uvScale;
} prop;
layout(set = 2) uniform sampler2D noiseTex;

#define USE_PERLIN_NOISE_3D
#define NOISE_WHITE_NOISE_TEX noiseTex
#define USE_WORLEY_NOISE_3D
#include "Lib/Noise.glsl"
#include "Lib/Util.glsl"

float CloudDensity(vec3 pos)
{
    pos += vec3(0,0,g.time/2);
    //
    // Initial values
    vec4 frequency = prop.worley_freq;
    float perlin = 0;
    float worley = 0;

    vec4 amp = prop.perlin0_amp;
    perlin += amp.x * (perlinNoise3D(pos * prop.perlin0_freq.x) * 0.5 + 0.5);
    perlin += amp.y * (perlinNoise3D(pos * prop.perlin0_freq.y) * 0.5 + 0.5);
    perlin += amp.z * (perlinNoise3D(pos * prop.perlin0_freq.z) * 0.5 + 0.5);
    perlin += amp.w * (perlinNoise3D(pos * prop.perlin0_freq.w) * 0.5 + 0.5);
    
    worley += 0.625 * (1 - worley3D(frequency.x * pos));
    worley += 0.25 *  (1 - worley3D(frequency.y * pos));
    worley += 0.125 * (1 - worley3D(frequency.z * pos));

    float perlinWorley = remap(perlin, worley, 1, 0, 1);

    // perlin worley
    

    return max(perlinWorley, 0);
}

void main() {
    ivec3 coord = ivec3(gl_GlobalInvocationID.xyz);
    vec3 uv = coord / 128.0f * prop.uvScale;
    imageStore(imgOutput, coord, vec4(vec3(CloudDensity(uv)), 1.0f));
}

#endif


