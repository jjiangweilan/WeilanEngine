#version 450 core

#define PI 3.14159265359f
#if CONFIG
name: EnvironmentLightBaker
// interleaved: true
cull: none
#endif

struct VertToFrag { vec2 uv; };

layout(set = 0, binding = 0) uniform BakeInfo
{
    vec4 uFrom, uTo;
    vec4 vFrom, vTo;
    float roughness;
} bakeInfo;
layout(set = 0, binding = 1) uniform sampler2D environmentMap;

vec3 BakeInfo_GetViewDir(vec2 uv)
{
    vec3 dir = mix(mix(bakeInfo.uFrom, bakeInfo.uTo, uv.x),
           mix(bakeInfo.vFrom, bakeInfo.vTo, uv.x), uv.y).xyz;
    return dir;
}

float RadicalInverse_VdC(uint bits) 
{
    bits = (bits << 16u) | (bits >> 16u);
    bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
    bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
    bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
    bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
    return float(bits) * 2.3283064365386963e-10; // / 0x100000000
}
// ----------------------------------------------------------------------------
vec2 Hammersley(uint i, uint N)
{
    return vec2(float(i)/float(N), RadicalInverse_VdC(i));
}

const vec2 invAtan = vec2(0.1591, 0.3183);
vec2 CartesianToSpherical(vec3 v)
{
    // -asin because we have top-left uv origin
    vec2 uv = vec2(atan(v.z, v.x), -asin(v.y));

    // convert to [0,1]
    uv *= invAtan;
    uv += 0.5;
    return uv;
}

vec3 ImportanceSampleGGX(vec2 Xi, vec3 N, float roughness)
{
    float a = roughness*roughness;
	
    float phi = 2.0 * PI * Xi.x;
    float cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (a*a - 1.0) * Xi.y));
    float sinTheta = sqrt(1.0 - cosTheta*cosTheta);
	
    // from spherical coordinates to cartesian coordinates
    vec3 H;
    H.x = cos(phi) * sinTheta;
    H.y = sin(phi) * sinTheta;
    H.z = cosTheta;
	
    // from tangent-space vector to world-space sample vector
    vec3 up        = abs(N.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
    vec3 tangent   = normalize(cross(up, N));
    vec3 bitangent = cross(N, tangent);
	
    vec3 sampleVec = tangent * H.x + bitangent * H.y + N * H.z;
    return normalize(sampleVec);
}  

#if VERT

layout(location = 0) out VertToFrag v2f;

void main()
{
    vec4 tri[3] = {
        {-1, -3, 0.5, 1},
        {-1, 1, 0.5, 1},
        {3, 1, 0.5, 1},
    };
    vec4 pos = tri[gl_VertexIndex];

    v2f.uv = pos.xy;

    gl_Position = pos;
}
#endif

#if FRAG

#define sTexture sTexture_sampler_linear
layout(location = 0) in VertToFrag v2f;
layout(location = 0) out vec4 fragColor;

void main()
{
    vec2 uv = v2f.uv * 0.5 + 0.5;
    vec3 viewDir = BakeInfo_GetViewDir(uv);
    vec3 N = normalize(viewDir);
    vec3 V = N;

    const uint SAMPLE_COUNT = 1024u;
    float totalWeight = 0;
    vec3 prefilteredColor = vec3(0.0);
    for(uint i = 0u; i < SAMPLE_COUNT; ++i)
    {
        vec2 Xi = Hammersley(i, SAMPLE_COUNT);
        vec3 H  = ImportanceSampleGGX(Xi, N, bakeInfo.roughness);
        vec3 L  = normalize(2.0 * dot(V, H) * H - V);

        float NdotL = max(dot(N, L), 0.0);
        if(NdotL > 0.0)
        {
            vec2 uv = CartesianToSpherical(L);
            prefilteredColor += texture(environmentMap, uv).rgb * NdotL;
            totalWeight      += NdotL;
        }
    }
    prefilteredColor = prefilteredColor / totalWeight;

    fragColor = vec4(prefilteredColor, 1.0);
}
#endif
