#version 460

#if CONFIG
name: Billboard
depth:
    testEnable: false

#endif

#if VERT
#include "Common/SceneInfo.glsl"
layout(push_constant) uniform Transform
{
    vec4 position;
    vec4 scale;
} pconst;
layout(location = 0) out vec2 uv;
void main()
{
    vec4 c_Positions[6] =
        {
        { 1, 1, 0, 1 },
        { -1, -1, 0, 1 },
        { -1, 1, 0, 1 },

        { 1, -1, 0, 1 },
        { -1, -1, 0, 1 },
        { 1, 1, 0, 1 }
        };

    uv = c_Positions[gl_VertexIndex].xy * 0.5f + 0.5f;
    uv.y = 1 - uv.y;

    vec4 vPos = c_Positions[gl_VertexIndex];
    vPos.xy *= pconst.scale.xy;
    vec3 lookat = scene.viewPos.xyz - pconst.position.xyz;
    vec3 anyVec = lookat != vec3(0, 1, 0) ? vec3(0, 1, 0) : vec3(1, 1, 0);
    vec3 right = cross(lookat, anyVec);
    vec3 up = cross(right, lookat);

    mat4 billboardModelMatrix = {
        vec4(normalize(right.xyz), 0),
        vec4(normalize(up.xyz), 0),
        vec4(normalize(lookat.xyz), 0),
        vec4(pconst.position.xyz, 1)
        };
    gl_Position = scene.viewProjection * billboardModelMatrix * vPos;
}
#endif

#if FRAG
layout(location = 0) in vec2 uv;
layout(location = 0) out vec4 oColor;

layout(set = 0, binding = 1) uniform sampler2D mainTex;
void main()
{
    oColor = texture(mainTex, uv);
    if (oColor.a < 0.5)
        discard;

    oColor.a = 0.6;
}
#endif
